/**
 * Top-level build file for ftc_app project.
 *
 * It is extraordinarily rare that you will ever need to edit this file.
 */


buildscript {
    ext.kotlin_version = '1.5.0'
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
}


configurations {
    doc { transitive false }
}


// This is now required because aapt2 has to be downloaded from the
// google() repository beginning with version 3.2 of the Android Gradle Plugin
allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

repositories {
    mavenCentral()

    flatDir {
        dirs '../libs'
    }
}


dependencies {
    doc 'org.firstinspires.ftc:Hardware:6.2.1'
    doc 'org.firstinspires.ftc:RobotCore:6.2.1'
    doc 'org.firstinspires.ftc:FtcCommon:6.2.1'
    doc 'org.firstinspires.ftc:OnBotJava:6.2.1'
    doc 'org.firstinspires.ftc:Inspection:6.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task extractJavadoc {
    doLast {
        def componentIds = configurations.doc.incoming.resolutionResult.allDependencies.collect { it.selected.id }

        def result = dependencies.createArtifactResolutionQuery()
                .forComponents(componentIds)
                .withArtifacts(JvmLibrary, SourcesArtifact, JavadocArtifact)
                .execute()

        for (component in result.resolvedComponents) {
            component.getArtifacts(JavadocArtifact).each { artifact ->
                def version = artifact.identifier.componentIdentifier.version
                def libName = artifact.identifier.componentIdentifier.moduleIdentifier.name
                copy {
                    from zipTree(artifact.file)
                    into "docs/$version/$libName/"
                }
            }
        }
    }
}



compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}